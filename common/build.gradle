plugins {
    id 'java-library'
    id 'org.openapi.generator' version '7.5.0'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.5'
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-webflux'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    api 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    
    api 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
    implementation 'jakarta.validation:jakarta.validation-api'
    
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

def apiSpecDir = "${projectDir}/src/main/resources/api"
def generatedSourcesDir = "${buildDir}/generated/sources/openapi"
def generatedPackage = 'by.bsuir.growpathserver.common.dto'

sourceSets {
    main {
        java {
            srcDirs += "${generatedSourcesDir}/src/main/java"
        }
    }
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "${apiSpecDir}/auth-api.yaml"
    outputDir = "${generatedSourcesDir}"
    apiPackage = "${generatedPackage}.api"
    modelPackage = "${generatedPackage}.model"
    invokerPackage = "${generatedPackage}.invoker"
    
    configOptions = [
        dateLibrary: "java8",
        java8: "true",
        library: "native",
        useJakartaEe: "true",
        serializationLibrary: "jackson",
        openApiNullable: "true",
        useBeanValidation: "true",
        hideGenerationTimestamp: "true"
    ]
    
    typeMappings = [
        'OffsetDateTime': 'java.time.OffsetDateTime',
        'DateTime': 'java.time.OffsetDateTime'
    ]
    
    importMappings = [
        'OffsetDateTime': 'java.time.OffsetDateTime',
        'DateTime': 'java.time.OffsetDateTime'
    ]
    
    globalProperties = [
        models: "",
        apis: "",
        supportingFiles: ""
    ]
    
    skipValidateSpec = false
}

compileJava.dependsOn tasks.named('openApiGenerate')

tasks.named('compileJava') {
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true'
    ]
}

clean {
    delete "${generatedSourcesDir}"
}
